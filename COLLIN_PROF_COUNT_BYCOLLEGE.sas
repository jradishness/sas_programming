*********************************************

PROGRAM FOR CALCULATING THE TOTAL NUMBER OF 
SECTIONS TAUGHT BY PROFESSORS AT UNT DENTON,
COLLIN, AND INTERNET MEANS

				BY: JARED KELLY
				DATE: 8/29/19

********************************************;
libname unt1 "C:\Users\jrk0200\UNT System\Clark, Allen - FriscoEnrollment\EnrollmentRecords";

***IMPORT ARCHIVE;
DATA ARCHIVE;		*SPRING CENSUS;
	SET unt1.enrollement_dw_2019c_census unt1.enrollement_dw_2019z_census;
RUN;

*** FIND PROFESSORS;
DATA PROF_IDS;
	SET ARCHIVE;		* WE HAVE ALL ENROLLMENTS (SPRING/FALL) NOW;
	WHERE LOCATION IN ('FRSC', 'Z-CHEC', 'Z-INSPK');		*ONLY PROFESSORS WHO TEACH IN COLLIN;
	KEEP INSTR_EMPLID STRM;			* WE ONLY WANT THEIR ID'S. A LIST OF SORTS;
RUN;
PROC SORT DATA=PROF_IDS NODUPKEY;	*REMOVE DUPLICATES;
	BY INSTR_EMPLID STRM;
RUN;

* MERGE RELEVANT PROFESSORS WITH THEIR ENROLLMENTS (GRAB ONLY THE RELEVANT ENROLLMENTS);
/*DATA RELEVENROLL;		*COMBINE SPRING/FALL ENROLLMENTS;*/
/*	SET ARCHIVE;*/
/*RUN;*/
PROC SORT DATA=ARCHIVE OUT=RELEVENROLL;		*RESORT THE NEWLY-COMBINED DATASET;
	BY STRM INSTR_EMPLID;		
RUN;
DATA PROF_ENROLL;
	MERGE PROF_IDS (IN=A) RELEVENROLL;
	BY STRM INSTR_EMPLID;
	IF A=1;			*ONLY TAKE ENROLLMENTS FROM OUR PROFESSORS;
	KEEP STRM LOCATION COURSE INSTR_EMPLID INSTR_LAST_NAME INSTR_FIRST_NAME ACAD_GROUP;
RUN;

* CHANGE THE ID VAR AND BUILD THE NAME VAR;
DATA PROF_ENROLL;
	SET PROF_ENROLL;
	WHERE INSTR_EMPLID ~=" ";
	EMPLID = INSTR_EMPLID;
	NAME = CATX(' ', INSTR_FIRST_NAME, INSTR_LAST_NAME, ACAD_GROUP);
	KEEP STRM LOCATION COURSE EMPLID NAME ACAD_GROUP;
RUN;
PROC SORT DATA=PROF_ENROLL NODUPKEY;
	BY STRM COURSE;
RUN;
PROC SORT DATA=PROF_ENROLL;
	BY NAME;
RUN;


*ADD LOCATION COUNTERS; 
DATA PROF_ENROLL;
	SET PROF_ENROLL;
  	IF LOCATION IN ('FRSC', 'Z-CHEC', 'Z-INSPK') AND STRM = '1198' THEN F_CC_SECS=1;		*COLLIN COUNTY COURSES IN THE FALL;
	ELSE IF LOCATION IN ('FRSC', 'Z-CHEC', 'Z-INSPK') AND STRM = '1191' THEN S_CC_SECS=1;	*COLLIN COUNTY COURSES IN THE SPRING;
	ELSE IF LOCATION IN ('MAIN') AND STRM = '1198' THEN F_DT_SECS=1;	*MAIN COURSES IN THE FALL;
	ELSE IF LOCATION IN ('MAIN') AND STRM = '1191' THEN S_DT_SECS=1;	*MAIN COURSES IN THE SPRING;
	ELSE IF STRM = '1198' THEN F_OL_SECS=1;		*ONLINE COURSES IN THE FALL;
	ELSE S_OL_SECS=1;	*ONLINE COURSES IN THE SPRING;
RUN;

*COUNT LOCATION COUNTERS;
data F_DENTON_PROF;
	set PROF_ENROLL;
	by NAME;
	if First.NAME then DENTON_TOT = 0;
	DENTON_TOT + F_DT_SECS;
	IF LAST.NAME THEN OUTPUT;
	KEEP EMPLID DENTON_TOT;
run;
data F_COLLIN_PROF;
	set PROF_ENROLL;
	by NAME;
	if First.NAME then COLLIN_TOT = 0;
	COLLIN_TOT + F_CC_SECS;
	IF LAST.NAME THEN OUTPUT;
	KEEP EMPLID COLLIN_TOT;
run;
data F_INTERNET_PROF;
	set PROF_ENROLL;
	by NAME;
	if First.NAME then INTERNET_TOT = 0;
	INTERNET_TOT + F_OL_SECS;
	IF LAST.NAME THEN OUTPUT;
	KEEP EMPLID INTERNET_TOT;
run;
data S_DENTON_PROF;
	set PROF_ENROLL;
	by NAME;
	if First.NAME then SDENTON_TOT = 0;
	SDENTON_TOT + S_DT_SECS;
	IF LAST.NAME THEN OUTPUT;
	KEEP EMPLID SDENTON_TOT;
run;
data S_COLLIN_PROF;
	set PROF_ENROLL;
	by NAME;
	if First.NAME then SCOLLIN_TOT = 0;
	SCOLLIN_TOT + S_CC_SECS;
	IF LAST.NAME THEN OUTPUT;
	KEEP EMPLID SCOLLIN_TOT;
run;
data S_INTERNET_PROF;
	set PROF_ENROLL;
	by NAME;
	if First.NAME then SINTERNET_TOT = 0;
	SINTERNET_TOT + S_OL_SECS;
	IF LAST.NAME THEN OUTPUT;
	KEEP EMPLID SINTERNET_TOT;
run;

*PREPARE THE PROF DETAILS TO RE-MERGE FOR REPORTING;
PROC SORT DATA=PROF_ENROLL OUT=PROF NODUPKEY;		
	BY EMPLID ACAD_GROUP;
RUN;
PROC SORT DATA=F_COLLIN_PROF;
	BY EMPLID;
RUN;
PROC SORT DATA=F_DENTON_PROF;
	BY EMPLID;
RUN;
PROC SORT DATA=F_INTERNET_PROF;
	BY EMPLID;
RUN;
PROC SORT DATA=S_COLLIN_PROF;
	BY EMPLID;
RUN;
PROC SORT DATA=S_DENTON_PROF;
	BY EMPLID;
RUN;
PROC SORT DATA=S_INTERNET_PROF;
	BY EMPLID;
RUN;

DATA FINAL_PROF;
	MERGE PROF F_COLLIN_PROF F_DENTON_PROF F_INTERNET_PROF S_COLLIN_PROF S_DENTON_PROF S_INTERNET_PROF;
	BY EMPLID;
	DROP COURSE LOCATION F_CC_SECS F_DT_SECS F_OL_SECS S_CC_SECS S_DT_SECS S_OL_SECS;	*DROP EXTRANEOUS COUNTERS NOW THAT WE HAVE TOTALS;
RUN;

PROC SORT DATA=FINAL_PROF NODUPKEY;
	BY EMPLID ACAD_GROUP;
RUN;

PROC FORMAT;
	VALUE $ACAD_GRP 'ADMIN'='Administrative Group'
					'BUAD'='College of Business'
					'CLASS'='Col of Lib Arts & Social Sci'
					'COE'='College of Education'
					'COENG'='College of Engineering'
					'COS'='College of Science'
					'GRDSC'='Graduate School'
					'HNRS'='Honors College'
					'JOUR'='Mayborn School of Journalism'
					'LIBR'='College of Information'
					'MUSI'='College of Music'
					'NCL'='New College'
					'SMHM'='College of Mrch, Hosp, Tourism'
					'SOCS'='Col of Health & Public Service'
					'SOVA'='College of Visual Arts & Dsgn';
RUN;

ods tagsets.excelxp file="C:\Users\jrk0200\UNT System\Clark, Allen - FriscoEnrollment\Reports\2019_PROF_COUNT.xml" style=XLsansprinter
	options(embed_titles_once='yes' autofit_height='yes' sheet_interval='none'
			sheet_name='Professor Report' absolute_column_width='8,22,22,8,8,8,8,8,8');

*REPORT THE DATA;
PROC REPORT DATA=FINAL_PROF;
	COLUMN ('Collin Professor' EMPLID NAME ACAD_GROUP) 
	('Course Count'('Fall 2019' COLLIN_TOT DENTON_TOT INTERNET_TOT) 
	('Spring 2019' SCOLLIN_TOT SDENTON_TOT SINTERNET_TOT));
	DEFINE EMPLID / DISPLAY 'I.D.';
	DEFINE NAME / DISPLAY 'Name';
	DEFINE ACAD_GROUP / DISPLAY 'College' FORMAT=$ACAD_GRP.;
	DEFINE COLLIN_TOT / ANALYSIS 'Collin';
	DEFINE DENTON_TOT / ANALYSIS 'Denton';
	DEFINE INTERNET_TOT / ANALYSIS 'Internet';
	DEFINE SCOLLIN_TOT / ANALYSIS 'Collin';
	DEFINE SDENTON_TOT / ANALYSIS 'Denton';
	DEFINE SINTERNET_TOT / ANALYSIS 'Internet';
	RBREAK AFTER / SUMMARIZE;
RUN;
ods tagsets.excelxp close;
title;



data arland;
	set archive;
	where instr_emplid = "10035524";
run;

proc freq data=arland;
	table course*strm;
run;

PROC SORT DATA=PROF_IDS;
	BY INSTR_EMPLID;
RUN;
