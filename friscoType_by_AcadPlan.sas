*** proc freq of acadplans by Frisco_type ;

libname enroll "S:\UIS\Shared\sas_data_projects\enrollment_records";
libname simst "S:\UIS\Shared\sas_data_projects\simsterm_records";

* Gather Frisco Students;
DATA ENROLLMENT;
	SET enroll.Enrollement_dw_2020z_062220;
RUN;
PROC SORT DATA=ENROLLMENT;
	BY EMPLID;
RUN;
data frisco_ids;
	set ENROLLMENT;
	where location in ("Z-CHEC", 'Z-INSPK', 'FRSC');
	keep emplid;
RUN;
*REMOVE DUPLICATES;
PROC SORT DATA=FRISCO_IDS NODUPKEY;
	BY EMPLID;
RUN;
*ADD ACAD_PLAN;
DATA FRISCO_STUDENTS;
	MERGE FRISCO_IDS (IN=A) SIMST.S2020z_062220;
	BY EMPLID;
	IF A=1;
	KEEP EMPLID ACAD_PLAN;
RUN;
*Separately collect Frisco Enrollment (drop acad_plan);
DATA FRISCO_STUD_ENROLL;
	MERGE FRISCO_IDS (IN=A) ENROLLMENT;
	BY EMPLID;
	IF A=1;
	DROP ACAD_PLAN;
RUN;
DATA FRISCO_STUD_ENROLL2;
	SET FRISCO_STUD_ENROLL;
	IF LOCATION IN ("Z-CHEC", "Z-INSPK", "FRSC") THEN FRISCO = 1;
	ELSE IF LOCATION NOT IN ('FRSC', 'Z-CHEC', 'Z-INSPK', 'Z-INET-OS', 'Z-INET-TX', 'Z-AOP') THEN OTHERS = 1;
RUN;
DATA FRISCO_COUNT;
	SET FRISCO_STUD_ENROLL2;
	BY EMPLID;
	IF FIRST.EMPLID THEN FRSC_CT = FRISCO;
	ELSE FRSC_CT + FRISCO;
	IF LAST.EMPLID THEN OUTPUT;
	KEEP EMPLID FRSC_CT;
RUN;
DATA OTHER_COUNT;
	SET FRISCO_STUD_ENROLL2;
	BY EMPLID;
	IF FIRST.EMPLID THEN OTHR_CT = OTHERS;
	ELSE OTHR_CT + OTHERS;
	IF LAST.EMPLID THEN OUTPUT;
	KEEP EMPLID OTHR_CT;
RUN;
DATA FRISCO_IDS_W_CT;
	MERGE FRISCO_IDS (IN=A) FRISCO_COUNT OTHER_COUNT;
	BY EMPLID;
RUN;
DATA FRISCO_TYPES;
	SET FRISCO_IDS_W_CT;
	LENGTH FTYPE $16;
	IF OTHR_CT = . THEN FTYPE = "Frisco_Only";
	ELSE IF FRSC_CT > OTHR_CT THEN FTYPE = "Frisco_Majority"; * CHANGE TO ge IF THEY WANT MORE OBS;
	ELSE FTYPE = "Frisco_Touch";
	KEEP EMPLID FTYPE;
RUN;
DATA FRISCO_FINAL_COUNTS;
	MERGE FRISCO_STUDENTS FRISCO_TYPES;
	BY EMPLID;
RUN;

ods pdf file="C:\Users\jrk0200\UNT System\Special Projects Frisco Data Team - General\acad_plan_by_type.pdf";
TITLE1 "Cross-Tabulation of Frisco Students";
TITLE2 "Frisco Student Type by Academic Plan";
PROC FREQ DATA=FRISCO_FINAL_COUNTS ORDER=FREQ;
	TABLE ACAD_PLAN*FTYPE / NOCUM NOPERCENT NOROW NOCOL;
RUN;
ods pdf close;	


